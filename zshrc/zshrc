# .zshrc
# Matthew's zsh settings

# ----------------------------------------
# Basics
# ----------------------------------------

# Aliases
alias ls='ls -G'
alias ll='ls -lG'

# Shell options
setopt ignore_eof               # Do not close window with Ctrl-D

# History
SAVEHIST=20000
HISTSIZE=10000

setopt EXTENDED_HISTORY         # save timestamp and command elapsed time
setopt APPEND_HISTORY           # append to history instead of overwriting
unsetopt INC_APPEND_HISTORY     # ... but when sessions are closed, not after each command
unsetopt SHARE_HISTORY          # (turned off) read history whenever used

setopt HIST_VERIFY              # require editing/verifying after !!
setopt HIST_FIND_NO_DUPS        # ignore duplicates in history search
setopt HIST_IGNORE_DUPS         # don't save consecutive duplicates
hgrep () { fc -Dlim "*$@*" 1 }
# ... consider using fzf instead

# ----------------------------------------
# Keyboard bindings
# ----------------------------------------

bindkey "\e[1~" beginning-of-line
bindkey "\e[7~" beginning-of-line
bindkey "\e[H"  beginning-of-line
bindkey "\eOH"  beginning-of-line

bindkey "\e[4~" end-of-line
bindkey "\e[8~" end-of-line
bindkey "\e[F"  end-of-line
bindkey "\eOF"  end-of-line

bindkey "\e[5~" beginning-of-history
bindkey "\e[6~" end-of-history


# ----------------------------------------
# Prompt
# ----------------------------------------

# Enable prompt substitution
setopt prompt_subst
autoload -Uz add-zsh-hook

# Git prompt component: $vcs_info_msg_0_
autoload -Uz vcs_info
precmd_vcs_info() { vcs_info }
add-zsh-hook precmd precmd_vcs_info
zstyle ':vcs_info:git:*' formats ' %b'                      # space in front if populated

# Virtualenv prompt component: $prompt_virtualenv
export VIRTUAL_ENV_DISABLE_PROMPT=1
function precmd_virtualenv_info() {
    if [[ -z $VIRTUAL_ENV ]] then
        prompt_virtualenv=''
    else
        prompt_virtualenv=" ${VIRTUAL_ENV##*/}"             # space in front if populated
    fi
}
add-zsh-hook precmd precmd_virtualenv_info

# Title bar prompt component
function precmd_window_title() {
    echo -ne "\e]1;$USER@$(hostname) $PWD\a"
}
add-zsh-hook precmd precmd_window_title

# Build the prompt
# The default is PS1="%n@%m %1~ %# "
NEWPROMPT="%n@%m"                                           # default:   username@host
NEWPROMPT="${NEWPROMPT} %F{12}%1~%f"                        # blue:      pwd
NEWPROMPT="${NEWPROMPT}%F{14}\$vcs_info_msg_0_%f"           # cyan:      branch (may be empty)
NEWPROMPT="${NEWPROMPT}%F{11}\$prompt_virtualenv%f"         # yellow:    virtualenv (may be empty)
NEWPROMPT="${NEWPROMPT} %F{8}%!%f"                          # grey:      histnum
NEWPROMPT="${NEWPROMPT} %(?.%F{green}%#.%F{red}%#)%f "      # green/red: $ or %
PS1=${NEWPROMPT}


# ----------------------------------------
# Additional zsh configuration
# ----------------------------------------

# Tab highlights and completions
# https://stackoverflow.com/questions/8300687/zsh-color-partial-tab-completions
# ... Show the interactive menu, available if you press Tab:
zstyle ':completion:*' menu select
# ... Show matched letters in blue and the next active selection letter in magenta
#     but handwave away the consistency details with LS_COLORS for now
zstyle -e ':completion:*:default' list-colors 'reply=("${PREFIX:+=(#bi)($PREFIX:t)(?)*==34=35}:${(s.:.)LS_COLORS}")'


# ----------------------------------------
# Optional software
# ----------------------------------------

# Visual Studio Code CLI
export PATH="$PATH:/Applications/Visual Studio Code.app/Contents/Resources/app/bin"

# Google Cloud SDK path and shell completion
if [ -f '/opt/google-cloud-sdk/path.zsh.inc' ]; then . '/opt/google-cloud-sdk/path.zsh.inc'; fi
if [ -f '/opt/google-cloud-sdk/completion.zsh.inc' ]; then . '/opt/google-cloud-sdk/completion.zsh.inc'; fi

# nvm
export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm
[ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"  # This loads nvm bash_completion

# Java
if [ -f /usr/libexec/java_home ]; then
    export JAVA_HOME="`/usr/libexec/java_home -v 1.8`" 2>/dev/null
fi

# Git
alias gitlog="git log --pretty=format:'%C(yellow)%h %Cred%ad %Cblue%an%Cgreen%d %Creset%s' --date=short"

# ----------------------------------------
# Local customizations
# ----------------------------------------

if [ -e ~/.zshrc.local ]; then
    source ~/.zshrc.local
fi

